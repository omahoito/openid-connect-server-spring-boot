apply plugin: 'maven-publish'

final UPLOAD_USERNAME_PROPERTY = "UPLOAD_USERNAME"
final UPLOAD_PASSWORD_PROPERTY = "UPLOAD_PASSWORD"

def getUsername = {
    System.getenv(UPLOAD_USERNAME_PROPERTY)
}

def getPassword = {
    System.getenv(UPLOAD_PASSWORD_PROPERTY)
}

task checkCredentials {
    doFirst {
        if (getUsername() == null || getPassword() == null) {
            throw new GradleException("Cannot find repository credentials. Please specify environment variables ${UPLOAD_USERNAME_PROPERTY} and ${UPLOAD_PASSWORD_PROPERTY}")
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }

    repositories {
        maven {
            url "https://oda-maven.solita.fi/repository/snapshots"
            credentials {
                username getUsername()
                password getPassword()
            }
        }
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/omahoito/omaolo"
            credentials {
                username = System.getenv("GITHUB_PACKAGES_DEPLOY_USERNAME")
                password = System.getenv("GITHUB_PACKAGES_DEPLOY_TOKEN")
            }
        }
    }
}


publish.dependsOn build
publish.dependsOn('checkCredentials')
publishToMavenLocal.dependsOn build

task deploy(dependsOn: ['build', 'publishToMavenLocal', 'publish'])
